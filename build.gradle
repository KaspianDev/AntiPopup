plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'com.github.kaspiandev'
version = '4.1'

repositories {
    mavenCentral()
    maven { url 'https://repo.codemc.io/repository/maven-snapshots/' }
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name = "No."
        url = "https://repo.viaversion.com"
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.19-R0.1-SNAPSHOT'
    compileOnly 'dev.dejvokep:boosted-yaml-spigot:1.3'
    compileOnly 'org.apache.commons:commons-configuration2:2.8.0'
    compileOnly "com.viaversion:viaversion:4.4.1"

    implementation 'com.github.retrooper.packetevents:spigot:2.0.0-20220807.223642-28'
}

tasks {
    build {
        dependsOn(shadowJar)
    }
    shadowJar {
        archiveName = 'AntiPopup.jar'

        minimize()
        relocate("io.github.retrooper.packetevents", "com.github.kaspiandev.antipopup.libs.io.github.retrooper.packetevents")
        relocate("com.github.retrooper.packetevents", "com.github.kaspiandev.antipopup.libs.com.github.retrooper.packetevents")
        dependencies {
            exclude(dependency('com.google.code.gson:gson:2.8.0'))
        }
    }
}

def targetJavaVersion = 17

jar.enabled = false

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
