plugins {
    id "java"
    id "io.github.goooler.shadow" version "${shadowVer}"
}

repositories {
    maven {
        name = "devmart-other"
        url = "https://nexuslite.gcnt.net/repos/other/"
    }
}

dependencies {
    implementation project(path: ":shared")
    implementation project(path: ":nms")
    implementation project(path: ":v1.19.2", configuration: "reobf")
    implementation project(path: ":v1.19.3", configuration: "reobf")
    implementation project(path: ":v1.19.4", configuration: "reobf")
    implementation project(path: ":v1.20.1", configuration: "reobf")
    implementation project(path: ":v1.20.2", configuration: "reobf")
    implementation project(path: ":v1.20.4", configuration: "reobf")
    implementation project(path: ":v1.20.6", configuration: "reobf")
    implementation project(path: ":v1.21", configuration: "reobf")
    implementation project(path: ":v1.21.2", configuration: "reobf")
    implementation project(path: ":v1.21.4", configuration: "reobf")
    implementation project(path: ":v1.21.5", configuration: "reobf")
    implementation project(path: ":v1.21.6", configuration: "reobf")
    implementation project(path: ":v1.21.9", configuration: "reobf")
    implementation project(path: ":v1.21.11", configuration: "default")

    compileOnly "org.spigotmc:spigot-api:1.19.4-R0.1-SNAPSHOT"
    compileOnly "com.viaversion:viaversion:5.4.0"
    compileOnly "org.apache.logging.log4j:log4j-core:2.19.0"

    implementation "org.bstats:bstats-bukkit:3.0.0"
    implementation "com.github.retrooper:packetevents-spigot:${packetEventsVer}"
    implementation "com.tcoded:FoliaLib:0.2.3"
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

tasks {
    build {
        dependsOn(shadowJar)
    }
    shadowJar {
        minimize()
        relocate("io.github.retrooper.packetevents", "com.github.kaspiandev.antipopup.libs.packetevents.spigot")
        relocate("com.github.retrooper.packetevents", "com.github.kaspiandev.antipopup.libs.packetevents.api")
        relocate("net.kyori", "com.github.kaspiandev.antipopup.libs.kyori")
        relocate("org.bstats", "com.github.kaspiandev.antipopup.libs.bstats")
        relocate("com.tcoded.folialib", "com.github.kaspiandev.antipopup.libs.folialib")
        
        manifest {
            attributes([
                "paperweight-mappings-namespace": "mojang"
            ])
        }
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset "UTF-8"
    filesMatching("plugin.yml") {
        expand props
    }
}
